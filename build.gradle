buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6
version = '1.4.20'
group = 'com.getbase'

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile group: "org.glassfish.jersey.core", name: "jersey-client", version: "2.17"
    compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.5.0"
    compile group: "joda-time", name: "joda-time", version: "2.3"
    compile group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-joda", version: "2.0.4"
    compile group: "org.slf4j", name: "slf4j-api", version: "1.7.12"

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: "org.spockframework", name: "spock-core", version: "1.0-groovy-2.4"
    testCompile group: "cglib", name: "cglib-nodep", version: "3.1"
    testCompile group: "org.objenesis", name: "objenesis-parent", version: "1.2"
    testCompile group: "ch.qos.logback", name: "logback-classic", version: "1.1.3"
    testCompile group: "com.google.guava", name:"guava", version:"23.0"
}

def getBintrayUsername() {
    return hasProperty('BINTRAY_USERNAME') ? BINTRAY_USERNAME : ""
}

def getBintrayPassword() {
    return hasProperty('BINTRAY_PASSWORD') ? BINTRAY_PASSWORD : ""
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = getBintrayUsername()
    key = getBintrayPassword()

    publications = ['mavenJava']

    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        userOrg = 'basecrm'
        name = 'basecrm-java'
        desc = 'BaseCRM Official API V2 library client for Java'
        websiteUrl = 'https://github.com/basecrm/basecrm-java'
        issueTrackerUrl = 'https://github.com/basecrm/basecrm-java/issues'
        vcsUrl = 'https://github.com/basecrm/basecrm-java.git'
        licenses = ['Apache License 2.0']
        labels = ['base crm', 'api', 'http', 'v2', 'client', 'HTTP client']
        publicDownloadNumbers = true
    }
}

test {
    if (!System.getenv('BASECRM_ACCESS_TOKEN')) {
        //put your access token here
        environment 'BASECRM_ACCESS_TOKEN': ''
    }
}
